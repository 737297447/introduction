apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

group = 'com.github.rubengees'

android {
    compileSdkVersion 24
    buildToolsVersion "$build_tools_version"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 24
        versionCode 18
        versionName "1.3.2"
        consumerProguardFiles 'proguard-rules.pro'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile "com.android.support:appcompat-v7:$support_version"
    compile "com.android.support:percent:$support_version"
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.getDestinationDir()
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
